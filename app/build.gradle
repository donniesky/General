plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

final File PROD_PROPS_FILE = new File(System.getProperty('user.home'), '.sign/signing.properties')
final File REPO_PROPS_FILE = new File('repo.properties')
final Properties PROD_PROPS = loadProperties(PROD_PROPS_FILE)
final Properties REPO_PROPS = loadProperties(REPO_PROPS_FILE)

private setSigningConfigKey(config, Properties props) {
    if(props != null) {
        config.storeFile = props['keystore'] == null ? null : file(props['keystore'])
        config.storePassword = props['store.pass']
        config.keyAlias = props['key.alias']
        config.keyPassword = props['key.pass']
    }
    return config
}

private static Properties loadProperties(File file) {
    Properties props = null
    if (file.canRead()) {
        props = new Properties()
        props.load(new FileInputStream(file))
    } else {
        System.err.println "\"${file}\" not found"
    }
    return props
}


def static getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd')
    return formattedDate
}

def computeVersionName(label) {
    return "0.0.${android.defaultConfig.versionCode}-${label}-${date}"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    dexOptions {
        preDexLibraries = true
        jumboMode = true
    }

    dataBinding {
        enabled = true
    }

    viewBinding {
        enabled = true
    }

    flavorDimensions 'default'

    defaultConfig {
        applicationId "me.donnie.android.apps.general"
        testApplicationId "me.donnie.android.apps.general.test"
        minSdkVersion 25
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true

        dimension 'default'
        signingConfig signingConfigs.debug

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        prod { java.srcDirs += 'src/prod/java' }
        beta { java.srcDirs += 'src/beta/java' }
        alpha { java.srcDirs += 'src/alpha/java' }
        dev { java.srcDirs += 'src/dev/java' }
        custom { java.srcDirs += 'src/custom/java' }
    }

    signingConfigs {
        prod {
            setSigningConfigKey(prod, PROD_PROPS)
        }
        debug {
            setSigningConfigKey(debug, REPO_PROPS)
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile 'test-proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile 'test-proguard-rules.pro'
        }
    }

    productFlavors {
        dev {
            versionName computeVersionName('dev')
            applicationIdSuffix '.dev'
        }
        prod {
            versionName computeVersionName('prod')
            signingConfig signingConfigs.prod
        }
        alpha {
            versionName computeVersionName('alpha')
            applicationIdSuffix '.alpha'
        }
        beta {
            versionName computeVersionName('beta')
            applicationIdSuffix '.beta'
            signingConfig signingConfigs.prod
        }
        custom {
            versionName computeVersionName('custom')
            signingConfig signingConfigs.prod
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

apply from: '../gradle/src/test.gradle'
apply from: '../gradle/src/checkstyle.gradle'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2"
    implementation 'androidx.core:core-ktx:1.5.0-alpha05'
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha02'
    implementation 'androidx.startup:startup-runtime:1.0.0'
    implementation 'com.google.android.material:material:1.3.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0-alpha1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.0-beta01'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0-beta01'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.2'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}